plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.0-SNAPSHOT" apply false
    id "io.github.juuxel.loom-quiltflower" version "1.+" apply false // Quiltflower, a better decompiler
    id "io.github.p03w.machete" version "1.+" // automatic jar compressing on build
}

architectury {
    minecraft = rootProject.minecraft_version
}

ext {
    if (minecraft_version == '1.18.2') {
        // Mappings
        // https://lambdaurora.dev/tools/import_quilt.html
        qm_version = "26"
        // https://parchmentmc.org/docs/getting-started
        parchment_version = "2022.11.06"

        // Fabric
        // https://fabricmc.net/develop/
        fabric_loader_version = "0.14.11"
        fabric_api_version = "0.67.0+1.18.2"

        // Forge
        // https://files.minecraftforge.net/net/minecraftforge/forge/
        forge_version = "40.2.9"

        // Create - Fabric
        // https://modrinth.com/mod/create-fabric/versions
        create_fabric_version = "0.5.1-c-build.1142+mc1.18.2"

        // Create - Forge
        // https://github.com/Creators-of-Create/Create/wiki/Depending-on-Create
        create_forge_version = "0.5.1.c-297"
        registrate_forge_version = "MC1.18.2-1.1.3"
        flywheel_forge_minecraft_version = "1.18.2"
        flywheel_forge_version = "0.6.9-101"

        // Development QOL
        // Create Fabric supports all 3 recipe viewers: JEI, REI, and EMI. This decides which is enabled at runtime.
        // set to disabled to have none of them.
        fabric_recipe_viewer = "REI"
        // JEI - https://www.curseforge.com/minecraft/mc-mods/jei/files/all
        jei_fabric_version = "10.2.1.283"
        // JEI 10+ seems to have issues on Forge, so the versions must be different for now.
        jei_forge_version = "9.7.0.209"
        // REI - https://modrinth.com/mod/roughly-enough-items/versions
        rei_version = "8.3.583"
        // EMI - https://modrinth.com/mod/emi/versions
        emi_version = "0.5.3+1.18.2"

        // Mod Menu - https://modrinth.com/mod/modmenu/versions
        modmenu_version = "3.2.5"
        // LazyDFU - https://modrinth.com/mod/lazydfu/versions
        lazydfu_version = "0.1.2"
    } else if (minecraft_version == '1.19.2') {
        // Mappings
        // https://lambdaurora.dev/tools/import_quilt.html
        qm_version = "21"
        // https://parchmentmc.org/docs/getting-started
        parchment_version = "2022.11.27"

        // Fabric
        // https://fabricmc.net/develop/
        fabric_loader_version = "0.14.22"
        fabric_api_version = "0.76.0+1.19.2"

        // Forge
        // https://files.minecraftforge.net/net/minecraftforge/forge/
        forge_version = "43.2.21"

        // Create - Fabric
        // https://modrinth.com/mod/create-fabric/versions
        create_fabric_version = "0.5.1-c-build.1141+mc1.19.2"

        // Create - Forge
        // https://github.com/Creators-of-Create/Create/wiki/Depending-on-Create
        create_forge_version = "0.5.1.c-36"
        registrate_forge_version = "MC1.19-1.1.5"
        flywheel_forge_minecraft_version = "1.19.2"
        flywheel_forge_version = "0.6.9-18"

        // Development QOL
        // Create Fabric supports all 3 recipe viewers: JEI, REI, and EMI. This decides which is enabled at runtime.
        // set to disabled to have none of them.
        fabric_recipe_viewer = "REI"
        // JEI - https://www.curseforge.com/minecraft/mc-mods/jei/files/all
        jei_version = "11.2.0.254"
        // REI - https://modrinth.com/mod/roughly-enough-items/versions
        rei_version = "9.1.580"
        // EMI - https://modrinth.com/mod/emi/versions
        emi_version = "0.6.1+1.19.2"

        // Mod Menu - https://modrinth.com/mod/modmenu/versions
        modmenu_version = "4.1.2"
        // LazyDFU - https://modrinth.com/mod/lazydfu/versions
        lazydfu_version = "0.1.3"
    } else {
        throw new GradleException("Unexpected minecraft version")
    }
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "io.github.juuxel.loom-quiltflower"

    loom {
        silentMojangMappingsLicense()
    }

    repositories {
        mavenCentral()
        maven { url = "https://maven.shedaniel.me/" } // Cloth Config, REI
        maven { url = "https://dvs1.progwml6.com/files/maven/" } // JEI
        maven { url = "https://maven.parchmentmc.org" } // Parchment mappings
        maven { url = "https://maven.quiltmc.org/repository/release" } // Quilt Mappings
        maven { // Flywheel
            url = "https://maven.tterrag.com/"
            content {
                // need to be specific here due to version overlaps
                includeGroup("com.jozufozu.flywheel")
            }
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${minecraft_version}"
        // layered mappings - Mojmap names, parchment and QM docs and parameters
        mappings(loom.layered {
            it.mappings("org.quiltmc:quilt-mappings:${minecraft_version}+build.${qm_version}:intermediary-v2")
            it.parchment("org.parchmentmc.data:parchment-${minecraft_version}:${parchment_version}@zip")
            it.officialMojangMappings { nameSyntheticMembers = false }
        })

        // see each subproject for dependencies.
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    group = rootProject.maven_group

    // Formats the mod version to include the loader, Minecraft version, and build number (if present)
    // example: 1.0.0+fabric-1.18.2-100
    String buildNumber = System.getenv("GITHUB_RUN_NUMBER")
    version = "${mod_version}+${project.name}-${minecraft_version}" + (buildNumber != null ? "-${buildNumber}" : "")

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-Xplugin:Manifold', "-AMC_${minecraft_version.tokenize('.')[1]}"]
    }

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
    }

    dependencies {
        // Add manifold to -processorpath for javac
        annotationProcessor 'systems.manifold:manifold-preprocessor:2023.1.14'
        testAnnotationProcessor 'systems.manifold:manifold-preprocessor:2023.1.14'
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    java {
        withSourcesJar()
    }

    machete {
        // disable machete locally for faster builds
        enabled = buildNumber != null
    }
}