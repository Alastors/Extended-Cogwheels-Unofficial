plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.0-SNAPSHOT" apply false
    id "io.github.juuxel.loom-quiltflower" version "1.+" apply false // Quiltflower, a better decompiler
    id "io.github.p03w.machete" version "1.+" // automatic jar compressing on build
}

architectury {
    minecraft = rootProject.minecraft_version
}

ext {
    // Since a pre-processor is used to handle version differences only one
    // branch is used. To practically handle multiple versions dependencies
    // are set here based on the selected minecraft version in `gradle.properties`

    // Mappings - https://lambdaurora.dev/tools/import_quilt.html
    // Parchment - https://parchmentmc.org/docs/getting-started
    // Fabric - https://fabricmc.net/develop/
    // Forge - https://files.minecraftforge.net/net/minecraftforge/forge/
    // Create (Fabric) - https://modrinth.com/mod/create-fabric/versions
    // Create (Forge) - https://maven.tterrag.com/com/simibubi/create/
    // JEI - https://www.curseforge.com/minecraft/mc-mods/jei/files/all
    // REI - https://modrinth.com/mod/roughly-enough-items/versions
    // EMI - https://modrinth.com/mod/emi/versions
    // Mod Menu - https://modrinth.com/mod/modmenu/versions"
    // LazyDFU - https://modrinth.com/mod/lazydfu/versions

    if (minecraft_version == '1.18.2') {
        qm_version = "26"

        parchment_version = "2022.11.06"

        fabric_loader_version = "0.14.11"
        fabric_api_version = "0.67.0+1.18.2"

        forge_version = "40.2.9"

        create_fabric_version = "0.5.1-c-build.1159+mc1.18.2"

        create_forge_version = "0.5.1.c-299"
        registrate_forge_version = "MC1.18.2-1.1.3"
        flywheel_forge_minecraft_version = "1.18.2"
        flywheel_forge_version = "0.6.9-101"

        fabric_recipe_viewer = "REI"
        jei_fabric_version = "10.2.1.283"
        jei_forge_version = "9.7.0.209"
        rei_version = "8.3.583"
        emi_version = "0.5.3+1.18.2"
        modmenu_version = "3.2.5"
        lazydfu_version = "0.1.2"
    } else if (minecraft_version == '1.19.2') {
        qm_version = "21"
        parchment_version = "2022.11.27"

        fabric_loader_version = "0.14.22"
        fabric_api_version = "0.76.0+1.19.2"

        forge_version = "43.2.21"

        create_fabric_version = "0.5.1-c-build.1141+mc1.19.2"

        create_forge_version = "0.5.1.c-36"
        registrate_forge_version = "MC1.19-1.1.5"
        flywheel_forge_minecraft_version = "1.19.2"
        flywheel_forge_version = "0.6.9-18"

        fabric_recipe_viewer = "REI"
        jei_version = "11.2.0.254"
        rei_version = "9.1.580"
        emi_version = "0.6.1+1.19.2"
        modmenu_version = "4.1.2"
        lazydfu_version = "0.1.3"
    } else {
        throw new GradleException("Unexpected minecraft version")
    }
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "io.github.juuxel.loom-quiltflower"

    loom {
        silentMojangMappingsLicense()
    }

    repositories {
        mavenCentral()
        maven { url = "https://maven.shedaniel.me/" } // Cloth Config, REI
        maven { url = "https://modmaven.dev" } // JEI (backup since dvs1 is currently down)
        maven { url = "https://maven.parchmentmc.org" } // Parchment mappings
        maven { url = "https://maven.quiltmc.org/repository/release" } // Quilt Mappings
        maven { // Flywheel
            url = "https://maven.tterrag.com/"
            content {
                // need to be specific here due to version overlaps
                includeGroup("com.jozufozu.flywheel")
            }
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${minecraft_version}"
        // layered mappings - Mojmap names, parchment and QM docs and parameters
        mappings(loom.layered {
            it.mappings("org.quiltmc:quilt-mappings:${minecraft_version}+build.${qm_version}:intermediary-v2")
            it.parchment("org.parchmentmc.data:parchment-${minecraft_version}:${parchment_version}@zip")
            it.officialMojangMappings { nameSyntheticMembers = false }
        })

        // see each subproject for dependencies.
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    group = rootProject.maven_group

    // Formats the mod version to include the loader, Minecraft version, and build number (if present)
    // example: 1.0.0+fabric-1.18.2-100
    String buildNumber = System.getenv("GITHUB_RUN_NUMBER")
    version = "${mod_version}+${project.name}-${minecraft_version}" + (buildNumber != null ? "-${buildNumber}" : "")

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-Xplugin:Manifold', "-AMC_${minecraft_version.tokenize('.')[1]}"]
    }

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
    }

    dependencies {
        annotationProcessor 'systems.manifold:manifold-preprocessor:2023.1.14'
        testAnnotationProcessor 'systems.manifold:manifold-preprocessor:2023.1.14'

        annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    java {
        withSourcesJar()
    }

    machete {
        // disable machete locally for faster builds
        enabled = buildNumber != null
    }
}